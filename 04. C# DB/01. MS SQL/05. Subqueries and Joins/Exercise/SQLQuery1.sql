-- Part I – Queries for SoftUni Database

-- 01. Employee Address
  SELECT TOP(5)
	     [e].[EmployeeID],
	     [e].[JobTitle],
	     [a].[AddressID],
	     [a].[AddressText]
	FROM [Employees] AS [e]
	JOIN [Addresses] AS [a] ON [e].[AddressID] = [a].[AddressID]
ORDER BY [a].[AddressID] ASC

-- 02. Addresses with Towns
  SELECT TOP(50)
	     [e].[FirstName],
	     [e].[LastName],
	     [t].[Name],
	     [a].[AddressText]
	FROM [Employees] AS [e]
	JOIN [Addresses] AS [a] ON [e].[AddressID] = [a].[AddressID]
	JOIN [Towns] AS [t] ON [a].[TownID] = [t].[TownID]
ORDER BY [e].[FirstName] ASC,
		 [e].[LastName] ASC

-- 03. Sales Employee
  SELECT [e].[EmployeeID],
		 [e].[FirstName],
		 [e].[LastName],
		 [d].[Name]
	FROM [Employees] AS [e]
	JOIN [Departments] AS [d] ON [e].[DepartmentID] = [d].[DepartmentID]
   WHERE [e].[DepartmentID] = 3 
ORDER BY [e].[EmployeeID] ASC

-- 04. Employee Departments
  SELECT TOP(5)
		 [e].[EmployeeID],
		 [e].[FirstName],
		 [e].[Salary],
		 [d].[Name]
	FROM [Employees] AS [e]
	JOIN [Departments] AS [d] ON [e].[DepartmentID] = [d].[DepartmentID]
   WHERE [e].[Salary] > 15000
ORDER BY [d].[DepartmentID] ASC

-- 05. Employees Without Project
   SELECT TOP(3) 
		  [e].[EmployeeID],
		  [e].[FirstName]
	 FROM [Employees] AS [e]
LEFT JOIN [EmployeesProjects] AS [ep] ON [e].[EmployeeID] = [ep].[EmployeeID]
    WHERE [ep].[ProjectID] IS NULL
 ORDER BY [e].[EmployeeID]

-- 06. Employees Hired After
  SELECT [e].[FirstName],
		 [e].[LastName],
		 [e].[HireDate],
		 [d].[Name]
	FROM [Employees] AS [e]
	JOIN [Departments] AS [d] ON [e].[DepartmentID] = [d].[DepartmentID]
   WHERE [e].[HireDate] >= '1999'
     AND [e].[DepartmentID] IN (3, 10)
ORDER BY [e].[HireDate] ASC

-- 07. Employees with Project
  SELECT TOP(5)
		 [e].[EmployeeID],
		 [e].[FirstName],
		 [p].[Name]
	FROM [EmployeesProjects] AS [ep]
	JOIN [Employees] AS [e] ON [ep].[EmployeeID] = [e].[EmployeeID]
	JOIN [Projects] AS [p] ON [ep].[ProjectID] = [p].[ProjectID]
   WHERE [p].[StartDate] >= '08.13.2002'
     AND [p].[EndDate] IS NULL
ORDER BY [e].[EmployeeID]

-- 08. Employee 24
SELECT [e].[EmployeeID],
	   [e].[FirstName],
	   CASE
			WHEN [p].[StartDate] < '2005' THEN [p].[Name]
			ELSE NULL
	   END AS [ProjectName]
  FROM [EmployeesProjects] AS [ep]
  JOIN [Employees] AS [e] ON [ep].[EmployeeID] = [e].[EmployeeID]
  JOIN [Projects] AS [p] ON [ep].[ProjectID] = [p].[ProjectID]
 WHERE [e].[EmployeeID] = 24

-- 09. Employee Manager
  SELECT [e].[EmployeeID],
		 [e].[FirstName],
		 [m].[EmployeeID] AS [ManagerID],
		 [m].[FirstName] AS [ManagerName]
	FROM [Employees] AS [e]
	JOIN [Employees] AS [m] ON [e].[ManagerID] = [m].[EmployeeID]
   WHERE [e].[ManagerID] IN (3, 7)
ORDER BY [e].[EmployeeID] ASC

-- 10. Employees Summary
  SELECT TOP(50)
		 [e].[EmployeeID],
		 CONCAT_WS(' ', [e].[FirstName], [e].[LastName]) AS [EmployeeName],
		 CONCAT_WS(' ', [m].[FirstName], [m].[LastName]) AS [ManagerName],
		 [d].[Name] AS [DepartmentName]
	FROM [Employees] AS [e]
	JOIN [Employees] AS [m] ON [e].[ManagerID] = [m].[EmployeeID]
	JOIN [Departments] AS [d] ON [e].[DepartmentID] = [d].[DepartmentID]
ORDER BY [e].[EmployeeID]

-- 11. Min Average Salary
SELECT TOP(1) AVG([e].[Salary]) AS [MinAverageSalary]
FROM [Employees] AS [e]
GROUP BY [e].[DepartmentID]
ORDER BY AVG([e].[Salary])

-- Part II – Queries for Geography Database

-- 12. Highest Peaks in Bulgaria
  SELECT [mc].[CountryCode],
		 [m].[MountainRange],
		 [p].[PeakName],
		 [p].[Elevation]
	FROM [MountainsCountries] AS [mc]
	JOIN [Mountains] AS [m] ON [mc].[MountainId] = [m].[Id]
	JOIN [Peaks] AS [p] ON [m].[Id] = [p].[MountainId]
   WHERE [mc].[CountryCode] = 'BG' 
     AND [p].[Elevation] > 2835
ORDER BY [p].[Elevation] DESC

-- 13. Count Mountain Ranges
  SELECT [CountryCode],
		 COUNT(MountainId) AS [MountainRanges]
	FROM [MountainsCountries]
GROUP BY [CountryCode]
  HAVING [CountryCode] IN ('US', 'RU', 'BG')

-- 14. Countries With or Without Rivers
   SELECT TOP(5)
		  [c].[CountryName],
		  [r].[RiverName]
	 FROM [Countries] AS [c]
LEFT JOIN [CountriesRivers] AS [cr] ON [c].[CountryCode] = [cr].[CountryCode]
LEFT JOIN [Rivers] AS [r] ON [cr].[RiverId] = [r].[Id]
	WHERE [c].[ContinentCode] = 'AF'
 ORDER BY [c].[CountryName] ASC

-- 15. * Continents and Currencies
WITH [CurrencyUsageByContinent] AS  (
	SELECT  [ContinentCode],
		    [CurrencyCode],
			COUNT([CurrencyCode]) AS [CurrencyUsage]
	  FROM [Countries]
  GROUP BY [CurrencyCode],[ContinentCode]
	HAVING COUNT([CurrencyCode]) > 1
),
[RankedCurrencyUsage] AS (
	SELECT [ContinentCode],
		   [CurrencyCode],
		   [CurrencyUsage], DENSE_RANK() OVER (PARTITION BY [ContinentCode] ORDER BY [CurrencyUsage] DESC) AS [Rank]
	  FROM [CurrencyUsageByContinent]
)

SELECT [ContinentCode],
	   [CurrencyCode],
	   [CurrencyUsage]
  FROM [RankedCurrencyUsage]
 WHERE [Rank] = 1

-- 16. Countries Without Any Mountains
   SELECT COUNT(*) AS [Count]
	 FROM [Countries] AS [c]
LEFT JOIN [MountainsCountries] AS [mc] ON [c].[CountryCode] = [mc].[CountryCode]
	WHERE [mc].[MountainId] IS NULL

-- 17. Highest Peak and Longest River by Country
WITH [HigestPeak] AS (
	   SELECT [c].[CountryCode],
			  MAX([p].[Elevation]) AS [HigestPeak]
		 FROM [Countries] AS [c]
	LEFT JOIN [MountainsCountries] AS [mc] ON [c].[CountryCode] = [mc].[CountryCode]
	LEFT JOIN [Peaks] AS [p] ON [mc].[MountainId] = [p].[MountainId]
	 GROUP BY [c].[CountryCode]
),
[LongestRiver] AS (
	   SELECT [c].[CountryCode],
			  MAX([r].[Length]) AS [LongestRiver]
		 FROM [Countries] AS [c]
	LEFT JOIN [CountriesRivers] AS [cr] ON [c].[CountryCode] = [cr].[CountryCode]
	LEFT JOIN [Rivers] AS [r] ON [cr].[RiverId] = [r].[Id]
	 GROUP BY [c].[CountryCode]
)

  SELECT TOP(5)
		 [c].[CountryName],
		 [p].[HigestPeak],
		 [r].[LongestRiver]
	FROM [Countries] AS [c]
	JOIN [HigestPeak] AS [p] ON [c].[CountryCode] = [p].[CountryCode]
	JOIN [LongestRiver] AS [r] ON [c].[CountryCode] = [r].[CountryCode]
ORDER BY [p].[HigestPeak] DESC,[r].[LongestRiver] DESC

-- 18. * Highest Peak Name and Elevation by Country
WITH [RankedMountains] AS (
	   SELECT [c].[CountryName],
			  [p].[PeakName],
			  [p].[Elevation],
			  [m].[MountainRange],
			  DENSE_RANK()  OVER (PARTITION BY [c].[CountryCode] ORDER BY [p].[Elevation] DESC) AS [Rank]
		 FROM [Countries] AS [c]
	LEFT JOIN [MountainsCountries] AS [mc] ON [c].[CountryCode] = [mc].[CountryCode]
	LEFT JOIN [Peaks] AS [p] ON [mc].[MountainId] = [p].[MountainId]
	LEFT JOIN [Mountains] AS [m] ON [mc].[MountainId] = [m].[Id]
)

  SELECT TOP(5)
		 [CountryName],
	     ISNULL([PeakName], '(no highest peak)') AS [Highest Peak Name],
	     ISNULL([Elevation], 0) AS [Highest Peak Elevation],
	     ISNULL([MountainRange], '(no mountain)') AS [Mountain]
	FROM [RankedMountains]
   WHERE [Rank] = 1
ORDER BY [CountryName] ASC,
		 [PeakName] DESC